buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.21"
    }
}
plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.5.21"
    id "com.jfrog.bintray" version "1.8.5"
    id "maven-publish"
    id "com.github.ben-manes.versions" version "0.39.0"
}

repositories {
    google()
    jcenter()
    mavenCentral()
}

apply from: 'buildsystem/ci.gradle'
apply from: 'buildsystem/dependencies.gradle'

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation apiDependencies.kotlinJdk
    implementation apiDependencies.kotlinCoroutines

    // Networking
    api apiDependencies.retrofit
    implementation apiDependencies.retrofitScalars
    implementation apiDependencies.gson
    implementation apiDependencies.gsonConverter
    implementation apiDependencies.loggingInterceptor

    // Reactive programming
    implementation apiDependencies.rxKotlin
    implementation apiDependencies.rxJava
    implementation apiDependencies.retrofitRxJavaAdapter

    // Testing
    testImplementation apiDependencies.junit
    testImplementation apiDependencies.mockito
}