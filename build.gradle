buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.21"
    }
}
plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.5.21"
    id "maven-publish"
    id "com.github.ben-manes.versions" version "0.39.0"
    id 'com.autonomousapps.dependency-analysis' version "0.76.0"
}

repositories {
    google()
    mavenCentral()
}

apply from: 'buildsystem/ci.gradle'
apply from: 'buildsystem/dependencies.gradle'

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    // Networking
    api apiDependencies.okhttp
    api apiDependencies.retrofit
    api apiDependencies.gson
    implementation apiDependencies.gsonConverter

    // Testing
    testImplementation apiDependencies.junit
    testImplementation apiDependencies.mockito
}