buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.30"
    }
}
plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.5.30"
    id "com.google.devtools.ksp" version "1.5.30-1.0.0"
    id "maven-publish"
    id "com.github.ben-manes.versions" version "0.39.0"
    id 'com.autonomousapps.dependency-analysis' version "0.76.0"
}

repositories {
    google()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

apply from: 'buildsystem/ci.gradle'

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    api 'com.squareup.retrofit2:retrofit:2.9.0'
    api 'com.squareup.moshi:moshi-kotlin:1.12.0'
    ksp 'dev.zacsweers.moshix:moshi-ksp:0.14.0'
}